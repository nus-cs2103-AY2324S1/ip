plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'checkstyle'
    id 'org.openjfx.javafxplugin' version '0.0.13'
}

checkstyle {
    toolVersion = '10.2'
}

repositories {
    mavenCentral()
}

javafx {
    version = '17.0.7'
    modules = ['javafx.base', 'javafx.controls', 'javafx.fxml', 'javafx.graphics']
}

dependencies {
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.10.0'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.10.0'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.10.1'

    // Used for loops to clean up the imports
    def platforms = ["win", "linux", "mac"]
    def javafxDependency = ["javafx-graphics", "javafx-controls", "javafx-fxml"]

    for (plt in platforms) {
        for (dep in javafxDependency) {
            runtimeOnly "org.openjfx:$dep:$javafx.version:$plt"
        }
    }
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"

        showExceptions true
        exceptionFormat "full"
        showCauses true
        showStackTraces true
        showStandardStreams = false
    }
}

application {
    mainClass.set("jarvis.Jarvis")
}

shadowJar {
    archiveBaseName = "jarvis"
    archiveClassifier = null
    dependsOn("distZip", "distTar")
}

run {
    standardInput = System.in
}

mainClassName = 'jarvis.Jarvis'
